"""
This contains rules for our own data processing libraries.
"""

# TODO(julia): add a rule for setting up Pyensembl cache, for now assuming it exists;
# will need to do something like
# export PYENSEMBL_CACHE_DIR='/biokepi/workdir/pyensembl-cache'
# pyensembl install --release 75 --species "homo sapiens"
# touch /biokepi/workdir/pyensembl-cache/b37decoy.cached
# (add this last one as input to the vaxrank rule later)

rule vaxrank:
  input:
    strelka = "{prefix}/strelka.vcf",
    mutect = "{prefix}/mutect.vcf",
    rna = "{prefix}/rna_final_sorted.bam",
    rna_index = "{prefix}/rna_final_sorted.bam.bai"
  output:
    ascii_report = "{prefix}/vaccine-peptide-report.txt",
    json_file = "{prefix}/vaccine-peptide-report.json",
    pdf_report = "{prefix}/vaccine-peptide-report.pdf",
  params:
    mhc_alleles = config["input"]["mhc_alleles"],
    mhc_predictor = "netmhcpan",
    vaccine_peptide_length = 25,
    padding_around_mutation = 5,
    max_vaccine_peptides_per_mutation = 3,
    min_mapping_quality = 1,
    min_variant_sequence_coverage = 1,
    min_alt_rna_reads = 3,
    mhc_epitope_lengths = "8-11"
  benchmark:
    "{prefix}/vaxrank_benchmark.txt"
  log:
    "{prefix}/vaxrank.log"
  run:
    mhc_alleles = ",".join(params.mhc_alleles)
    shell("""
        vaxrank \
        --vcf {input.strelka} \
        --vcf {input.mutect} \
        --bam {input.rna} \
        --mhc-predictor {params.mhc_predictor} \
        --mhc_alleles %s \
        --output-ascii-report {output.ascii_report} \
        --output-json-file {output.json_file} \
        --output-pdf-report {output.pdf_report} \
        --log-path {log} \
        --vaccine-peptide-length {params.vaccine_peptide_length} \
        --padding-around-mutation {params.padding_around_mutation} \
        --max-vaccine-peptides-per-mutation {params.max_vaccine_peptides_per_mutation} \
        --min-mapping-quality {params.min_mapping_quality} \
        --min-variant-sequence-coverage {params.min_variant_sequence_coverage} \
        --min-alt-rna-reads {params.min_alt_rna_reads} \
        --mhc-epitope-lengths {params.mhc_epitope_lengths}""" % mhc_alleles
    )
