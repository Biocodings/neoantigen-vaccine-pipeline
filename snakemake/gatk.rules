"""
This contains GATK-related processing rules.
"""

rule mark_dups:
  input:
    "{prefix}_merged_aligned_coordinate_sorted.bam"
  output:
    "{prefix}_aligned_coordinate_sorted_dups.bam"
  benchmark:
    "{prefix}_mark_dups_benchmark.txt"
  log:
    "{prefix}_mark_dups.log"
  resources:
    mem_mb = 20000
  shell:
    "TMPDIR={wildcards.prefix}_tmp "
    "MAX_SEQUENCES_FOR_DISK_READ_ENDS_MAP=50000 "
    "MAX_FILE_HANDLES_FOR_READ_ENDS_MAP=20000 "
    "SORTING_COLLECTION_SIZE_RATIO=0.250000 "
    "picard -Xmx20g -Djava.io.tmpdir={wildcards.prefix}_tmp "
    "MarkDuplicates "
    "INPUT={input} OUTPUT={output} "
    "VALIDATION_STRINGENCY=LENIENT METRICS_FILE={wildcards.prefix}_markdups_metrics.txt "
    "2> {log}"

rule sambamba_index_bam:
  input:
    "{prefix}.bam"
  output:
    "{prefix}.bam.bai"
  threads: _get_half_cores
  benchmark:
    "{prefix}_index_benchmark.txt"
  log:
    "{prefix}_index.log"
  shell:
    "sambamba index -t {threads} {input} {output} 2> {log}"

CHR = list(range(1, 23)) + ['X', 'Y', 'MT']

# TODO(julia): figure out how to combine with RNA IndelRealigner rules, very similar
rule dna_indel_realigner_target_creator_per_chr:
  input:
    expand("{{prefix}}/{type}_aligned_coordinate_sorted_dups.bam.bai", type=["normal", "tumor"]),
    normal = "{prefix}/normal_aligned_coordinate_sorted_dups.bam",
    tumor = "{prefix}/tumor_aligned_coordinate_sorted_dups.bam"
  output:
    "{prefix}/aligned_coordinate_sorted_dups_indelreal_{chr}.intervals"
  threads: _get_half_cores
  params:
    reference = config["reference"]["genome"]
  benchmark:
    "{prefix}/indel_realigner_target_creator_{chr}_benchmark.txt"
  log:
    "{prefix}/indel_realigner_target_creator_{chr}.log"
  resources:
    mem_mb = 20000
  shell:
    "gatk -Xmx20g -T RealignerTargetCreator -R {params.reference} "
    "--intervals {wildcards.chr} "
    "-I {input.normal} -I {input.tumor} -o {output} -nt {threads} "
    "--filter_reads_with_N_cigar --filter_mismatching_base_and_quals --filter_bases_not_stored "
    "2> {log}"

rule dna_indel_realigner_per_chr:
  input:
    expand("{{prefix}}/{type}_aligned_coordinate_sorted_dups.bam.bai", type=["normal", "tumor"]),
    normal = "{prefix}/normal_aligned_coordinate_sorted_dups.bam",
    tumor = "{prefix}/tumor_aligned_coordinate_sorted_dups.bam",
    intervals = "{prefix}/aligned_coordinate_sorted_dups_indelreal_{chr}.intervals"
  output:
    "{prefix}/normal_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam",
    "{prefix}/tumor_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam"
  params:
    reference = config["reference"]["genome"]
  benchmark:
    "{prefix}_indel_realigner_{chr}_benchmark.txt"
  log:
    "{prefix}_indel_realigner_{chr}.log"
  resources:
    mem_mb = 20000
  shell:
    "gatk -Xmx20g -T IndelRealigner -compress 0 -R {params.reference} "
    "-I {input.normal} -I {input.tumor} "
    "-targetIntervals {input.intervals} --intervals {wildcards.chr} "
    "--filter_reads_with_N_cigar --filter_mismatching_base_and_quals --filter_bases_not_stored "
    "--nWayOut _indelreal_chr_{wildcards.chr}.bam "
    "2> {log}"

rule dna_indel_realigner_merge:
  input:
    expand("{{prefix}}_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam", chr=CHR)
  output:
    "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam"
  benchmark:
    "{prefix}/dna_indel_realigner_merge_benchmark.txt"
  shell:
    "sambamba merge {output} {input}"

rule base_recalibrator:
  input:
    "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam"
  output:
    "{prefix}_aligned_coordinate_sorted_dups_indelreal_bqsr.table"
  params:
    reference = config["reference"]["genome"],
    known_sites = config["reference"]["dbsnp"]
  threads: _get_half_cores
  benchmark:
    "{prefix}_base_recalibrator_benchmark.txt"
  log:
    "{prefix}_base_recalibrator.log"
  resources:
    mem_mb = 20000
  shell:
    "gatk -Xmx20g -T BaseRecalibrator -nct {threads} -I {input} -R {params.reference} "
    "-knownSites {params.known_sites} -o {output} 2> {log}"

rule bqsr_print_reads:
  input:
    bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam",
    bqsr = "{prefix}_aligned_coordinate_sorted_dups_indelreal_bqsr.table"
  output:
    "{prefix}_aligned_coordinate_sorted_dups_indelreal_bqsr.bam"
  params:
    reference = config["reference"]["genome"]
  threads: _get_half_cores
  benchmark:
    "{prefix}_base_recalibrator_print_reads_benchmark.txt"
  log:
    "{prefix}_base_recalibrator_print_reads.log"
  resources:
    mem_mb = 20000
  shell:
    "gatk -Xmx20g "
    "-T PrintReads -nct {threads} -R {params.reference} -I {input.bam} -BQSR {input.bqsr} "
    "-o {output} 2> {log}"
